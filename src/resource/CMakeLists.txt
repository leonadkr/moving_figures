cmake_minimum_required( VERSION 4.1 )

project( resource LANGUAGES C )

find_package( PkgConfig REQUIRED )
pkg_check_modules( GIO2 REQUIRED gio-2.0 )

find_program( GLIB_COMPILE_RESOURCES NAMES glib-compile-resources REQUIRED )


string( JOIN "." RESOURCE_XML ${PROJECT_NAME} "xml" )
string( JOIN "." RESOURCE_C ${PROJECT_NAME} "c" )
string( JOIN "." RESOURCE_H ${PROJECT_NAME} "h" )

string( JOIN "_" RESOURCE_TARGET_SOURCE ${PROJECT_NAME} "target_source" )
string( JOIN "_" RESOURCE_TARGET_HEADER ${PROJECT_NAME} "target_header" )

configure_file( ${RESOURCE_XML} ${RESOURCE_XML} COPYONLY )


execute_process(
	COMMAND ${GLIB_COMPILE_RESOURCES} --generate-dependencies ${RESOURCE_XML}
	WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
	OUTPUT_VARIABLE	RESOURCE_DEPENDENCIES
)
string( REPLACE "\n" ";" RESOURCE_DEPENDENCIES ${RESOURCE_DEPENDENCIES} )
list( POP_BACK ${RESOURCE_DEPENDENCIES} )
list( TRANSFORM RESOURCE_DEPENDENCIES PREPEND ${CMAKE_CURRENT_SOURCE_DIR}/ )

add_custom_command( OUTPUT ${RESOURCE_C}
	WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
	COMMAND ${GLIB_COMPILE_RESOURCES}
	ARGS --sourcedir=${CMAKE_CURRENT_SOURCE_DIR} --generate-source --target=${RESOURCE_C} ${RESOURCE_XML}
	VERBATIM
	MAIN_DEPENDENCY	${RESOURCE_XML}
	DEPENDS ${RESOURCE_DEPENDENCIES}
)

add_custom_command( OUTPUT ${RESOURCE_H}
	WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
	COMMAND ${GLIB_COMPILE_RESOURCES}
	ARGS --sourcedir=${CMAKE_CURRENT_SOURCE_DIR} --generate-header --target=${RESOURCE_H} ${RESOURCE_XML}
	VERBATIM
	MAIN_DEPENDENCY	${RESOURCE_XML}
	DEPENDS ${RESOURCE_DEPENDENCIES}
)

add_custom_target( ${RESOURCE_TARGET_SOURCE}
	DEPENDS
		${RESOURCE_C}
)

add_custom_target( ${RESOURCE_TARGET_HEADER}
	DEPENDS
		${RESOURCE_H}
)

add_library( ${PROJECT_NAME} STATIC )
target_compile_features( ${PROJECT_NAME} PRIVATE c_std_17 )

target_sources( ${PROJECT_NAME}
	PRIVATE
		${CMAKE_CURRENT_BINARY_DIR}/${RESOURCE_C}

	PUBLIC
		FILE_SET publicHeaders
		TYPE HEADERS
		BASE_DIRS
			${CMAKE_CURRENT_BINARY_DIR}
		FILES
			${CMAKE_CURRENT_BINARY_DIR}/${RESOURCE_H}
)

add_dependencies( ${PROJECT_NAME}
	${RESOURCE_TARGET_SOURCE}
	${RESOURCE_TARGET_HEADER}
)

target_include_directories( ${PROJECT_NAME}
	PRIVATE
		${GIO2_INCLUDE_DIRS}
)

target_link_directories( ${PROJECT_NAME}
	PRIVATE
		${GIO2_LIBRARY_DIRS}
)

target_link_libraries( ${PROJECT_NAME}
	PRIVATE
		${GIO2_LIBRARIES}
)

